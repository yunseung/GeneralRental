apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.lotterental.generalrental"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(SIGNED_STORE_FILE)   // gradle.properties 파일 참조 앱 빌드키
            storePassword SIGNED_STORE_PASSWORD // gradle.properties 파일 참조 앱 빌드키
            keyAlias SIGNED_KEY_ALIAS           // gradle.properties 파일 참조 앱 빌드키
            keyPassword SIGNED_KEY_PASSWORD     // gradle.properties 파일 참조 앱 빌드키
            v1SigningEnabled true               // 앱빌드 방식 필수 값
            v2SigningEnabled true               // 앱빌드 방식 필수 값
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release    // 빌드 키 이용
            minifyEnabled true                     // 소스 축소
            zipAlignEnabled true                    // 스토어 올리기위한 zip 정렬

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //하단 부터 Build Variant에서 빌드선택후 빌드 하면 BuildConfig.Java파일에 변수가 생성됨.
            buildConfigField "boolean", "IS_DEV",  "false"
            buildConfigField "String",  "WEB_URL",  "\"https://oa.lotterental.net/oam/mobile/login/loginMain\""
            buildConfigField "String",  "API_URL", "\"\""
            buildConfigField "String", "APP_DOWNLOAD_URL", "\"http://124.243.70.42:8812/oam/mobile\""
        }

        debug{
            signingConfig signingConfigs.release
            minifyEnabled false

            //하단 부터 Build Variant에서 빌드선택후 빌드 하면 BuildConfig.Java파일에 변수가 생성됨
            buildConfigField "boolean", "IS_DEV",  "true"
            buildConfigField "String",  "WEB_URL",  "\"http://124.243.70.42:8812/oam/mobile/login/loginMain\""
            buildConfigField "String",  "API_URL", "\"http://124.243.70.42:8812/oam/mobile\""
            buildConfigField "String", "APP_DOWNLOAD_URL", "\"http://124.243.70.42:8812/mobile/main \""
        }
    }

    dataBinding {
        enabled = true
    }

    //여기서 생성된 앱의 이름을 바꿔준다
    def formattedDate = new Date().format('yyyyMMdd_HHmm')
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "general_rental_${variant.name}_${variant.versionName}_${formattedDate}.apk"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // 기본 필수
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // 퍼미션 체크
    implementation 'gun0912.ted:tedpermission:2.2.0'
    // barcode qr code
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
    implementation 'com.google.zxing:core:3.2.1'
    //
    implementation 'net.sourceforge.jexcelapi:jxl:2.6.12'
    //fcm
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-auth:16.0.1'
    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    // HttpClient 라이브러리.
    implementation 'com.android.volley:volley:1.1.0'
}

apply plugin: 'com.google.gms.google-services'